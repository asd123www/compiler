// use std::str::FromStr;
use crate::ast::*;

// lalrpop 里的约定
grammar;

// 约束 lexer 的行为
match {
    // 跳过空白符和注释
    r"\s*" => {},
    r"//[^\n\r]*[\n\r]*" => {},
    r"/\*[^\n\r]*[\n\r]*\*/" => {},
    // 剩下的情况采用默认方式处理
    _
}



// ------------------------------ Function ------------------------------------------

// CompUnit    ::= [CompUnit] (Decl | FuncDef)
pub CompUnit: CompUnit = {
    <func: DeclFuncPair> <mut funcs: (<DeclFuncPair>)*> => {
        funcs.insert(0, func); // param is the first.
        CompUnit {funcs}
    },
}

DeclFuncPair: DeclFuncPair = {
    <decl: Decl> => DeclFuncPair::Decl(decl),
    <func: FuncDef> => DeclFuncPair::Func(func),
}


// FuncDef     ::= FuncType IDENT "(" [FuncFParams] ")" Block;
FuncDef: FuncDef = {
    "int" <ident: Ident> "(" ")" <block: Block> => 
        FuncDef {func_type: 0, ident, block, params: None},
    "void" <ident: Ident> "("  ")" <block: Block> => 
        FuncDef {func_type: 1, ident, block, params: None},
        
    "int" <ident: Ident> "(" <param: FuncFParams> ")" <block: Block> => 
        FuncDef {func_type: 0, ident, block, params: Some(param)},
    "void" <ident: Ident> "(" <param: FuncFParams> ")" <block: Block> => 
        FuncDef {func_type: 1, ident, block, params: Some(param)},
}

// FuncDefFirstPart: FuncDefFirstPart {
//     <> <ident: Ident> "(" => 
// }

FuncType: FuncType = {
    "int" => FuncType::Int,
    "void" => FuncType::Void,
}


// FuncFParams ::= FuncFParam {"," FuncFParam};
FuncFParams: FuncFParams = {
    <param: FuncFParam> <mut params: ("," <FuncFParam>)*> => {
        params.insert(0, param); // param is the first.
        FuncFParams {params}
    },
}

// FuncFParam ::= BType IDENT ["[" "]" {"[" ConstExp "]"}]
FuncFParam: FuncFParam = {
    "int" <ident: Ident> => FuncFParam::Integer(ident),
    "int" <ident: Ident> "[" "]" <mut dims: ("[" <ConstExp> "]")*> => FuncFParam::Array(ident, dims),
}


// ------------------------------ body ------------------------------------------

// Block         ::= "{" {BlockItem} "}";
Block: Block = {
    "{" <mut items: (<BlockItem>)*> "}" => Block {items},   
}

// BlockItem     ::= Decl | Statement;
BlockItem: BlockItem = {
    <decl: Decl> => BlockItem::Decl(decl),
    <stmt: Statement> => BlockItem::Statement(stmt),
}

// statement: open_statement
//          | closed_statement
Statement: Statement = {
    <os :OpenStatement> => Statement::Open(os),
    <cs :ClosedStatement> => Statement::Closed(cs),
}

// open_statement: IF '(' expression ')' statement
//               | IF '(' expression ')' closed_statement ELSE open_statement
//               | "while" "(" Exp ")" Stmt
OpenStatement: OpenStatement = {
    "if" "(" <exp: Exp> ")" <s:Statement> => OpenStatement::If(exp, Box::new(s)),
    "if" "(" <exp: Exp> ")" <cs:ClosedStatement> "else" <os: OpenStatement> => 
            OpenStatement::Ifelse(exp, cs, Box::new(os)),
    "while" "(" <exp: Exp> ")" <stmt: Statement>  => OpenStatement::While(exp, Box::new(stmt)),
}

// closed_statement: non_if_statement
//                 | IF '(' expression ')' closed_statement ELSE closed_statement
ClosedStatement: ClosedStatement = {
    <stmt: Stmt> => ClosedStatement::Stmt(stmt),
    "if" "(" <exp: Exp> ")" <cs1: ClosedStatement> "else" <cs2: ClosedStatement> => 
            ClosedStatement::Ifelse(exp, Box::new(cs1), Box::new(cs2)),
}

// Stmt contains no if.
// Stmt ::= LVal "=" Exp ";"
//        | [Exp] ";"
//        | Block
//        | "return" [Exp] ";";
//        | "break" ";"
//        | "continue" ";"
Stmt: Stmt = {
    "return" <exp: Exp> ";" => Stmt::RetExp(exp),
    <lval: LVal> "=" <exp: Exp> ";" => Stmt::LvalExp(lval, exp),
    <exp: Exp> ";" => Stmt::SingleExp(exp),
    <block: Block> => Stmt::Block(block),
    ";" => Stmt::ZeroExp(),
    "break" ";" => Stmt::BreakKeyWord(),
    "continue" ";" => Stmt::ContinueKeyWord(),
}









// ------------------------------ Variable ------------------------------------------

// LVal ::= IDENT {"[" Exp "]"}
LVal: LVal = {
    <ident: Ident> <mut exps: ("[" <Exp> "]")*> => LVal {ident, exps}, 
}

// Decl          ::= ConstDecl | VarDecl;
Decl: Decl = {
    <constdecl: ConstDecl> => Decl::Constdecl(constdecl),
    <vardecl: VarDecl> => Decl::Vardecl(vardecl),
}

// ConstDecl     ::= "const" BType ConstDef {"," ConstDef} ";";
ConstDecl: ConstDecl = {
    "const" "int" <def: ConstDef> <mut constdefs: ("," <ConstDef>)*> ";"  => {
        constdefs.insert(0, def); // def is the first.
        ConstDecl {constdefs}
    },
}

// VarDecl       ::= BType VarDef {"," VarDef} ";";
VarDecl: VarDecl = {
    "int" <def: VarDef> <mut vardefs: ("," <VarDef>)*> ";" => {
        vardefs.insert(0, def);
        VarDecl {vardefs}
    }
}

// // BType         ::= "int";
// BType: BType = "int" => BType::Int;

// ConstDef      ::= IDENT {"[" ConstExp "]"} "=" ConstInitVal;
ConstDef: ConstDef = {
    <ident: Ident>  <mut dims: ("[" <ConstExp> "]")*>  "=" <constinitval: ConstInitVal> => 
            ConstDef{ident, dims, constinitval},
}

// VarDef ::= IDENT {"[" ConstExp "]"}
//      | IDENT {"[" ConstExp "]"} "=" InitVal;
VarDef: VarDef = {
    <ident: Ident> <mut dims: ("[" <ConstExp> "]")*> => VarDef::Ident(ident, dims),
    <ident: Ident> <mut dims: ("[" <ConstExp> "]")*> "=" <initval: InitVal> => VarDef::Identinitval(ident, dims, initval),
}


// ConstInitVal  ::= ConstExp | "{" [ConstInitVal {"," ConstInitVal}] "}"
ConstInitVal: ConstInitVal = {
    <constexp: ConstExp> => ConstInitVal::SingleExp(constexp),
    "{" "}" => ConstInitVal::ZeroInit(),
    "{" <fir: ConstInitVal> <mut vars: ("," <ConstInitVal>)*> "}" => {
        vars.insert(0, fir);
        ConstInitVal::MultiExp(vars)
    }
}

// InitVal ::= Exp 
//           | "{" [InitVal {"," InitVal}] "}"
InitVal: InitVal = {
    <exp: Exp> => InitVal::SingleExp(exp),
    "{" "}" => InitVal::ZeroInit(),
    "{" <fir: InitVal> <mut vars: ("," <InitVal>)*> "}" => {
        vars.insert(0, fir);
        InitVal::MultiExp(vars)
    }
}




// ------------------------------ Expression ------------------------------------------

// ConstExp      ::= Exp;
ConstExp: ConstExp = {
    <exp: Exp> => ConstExp{exp},
}

// LOrExp
Exp: Exp = {
    <lorexp: LOrExp> => Exp{lorexp},
}

// PrimaryExp    ::= "(" Exp ")" | LVal | Number;
PrimaryExp: PrimaryExp = {
    "(" <exp: Exp> ")" => PrimaryExp::Exp(Box::new(exp)),
    <lval: LVal> => PrimaryExp::Lval(lval),
    <num: Number> => PrimaryExp::Num(num),
}

// UnaryExp  ::= PrimaryExp 
//             | UnaryOp UnaryExp
//             | IDENT "(" [FuncRParams] ")"
UnaryExp: UnaryExp = {
    <primaryexp: PrimaryExp> => UnaryExp::Primaryexp(primaryexp),
    <unaryop: UnaryOp> <unaryexp: UnaryExp> => UnaryExp::Unaryexp(unaryop, Box::new(unaryexp)),

    <ident: Ident> "(" ")" => UnaryExp::Funcall(ident, None),
    <ident: Ident> "(" <params: FuncRParams> ")" => UnaryExp::Funcall(ident, Some(params)),
}

// FuncRParams ::= Exp {"," Exp};
FuncRParams: FuncRParams = {
    <param: Exp> <mut params: ("," <Exp>)*> => {
        params.insert(0, param); // param is the first.
        FuncRParams {params}
    },
}

// UnaryOp     ::= "+" | "-" | "!";
UnaryOp: UnaryOp = {
    "+" => UnaryOp::Add,
    "-" => UnaryOp::Sub,
    "!" => UnaryOp::Not,
}

// MulExp      ::= UnaryExp | MulExp ("*" | "/" | "%") UnaryExp;
MulExp: MulExp = {
    <unaryexp: UnaryExp> => MulExp::Unaryexp(unaryexp),
    <mulexp: MulExp> "*" <unaryexp: UnaryExp> => MulExp::Mulexp(Box::new(mulexp), unaryexp, "mul".to_string()),
    <mulexp: MulExp> "/" <unaryexp: UnaryExp> => MulExp::Divexp(Box::new(mulexp), unaryexp, "div".to_string()),
    <mulexp: MulExp> "%" <unaryexp: UnaryExp> => MulExp::Modexp(Box::new(mulexp), unaryexp, "mod".to_string()),
}

// AddExp      ::= MulExp | AddExp ("+" | "-") MulExp;
AddExp: AddExp = {
    <mulexp: MulExp> => AddExp::Mulexp(mulexp),
    <addexp: AddExp> "+" <mulexp: MulExp> => AddExp::Addexp(Box::new(addexp), mulexp, "add".to_string()),
    <addexp: AddExp> "-" <mulexp: MulExp> => AddExp::Subexp(Box::new(addexp), mulexp, "sub".to_string()),
}

// RelExp      ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;
RelExp: RelExp = {
    <addexp: AddExp> => RelExp::Addexp(addexp),
    <relexp: RelExp> "<" <addexp: AddExp>  => RelExp::Ltexp(Box::new(relexp), addexp, "lt".to_string()),
    <relexp: RelExp> ">" <addexp: AddExp>  => RelExp::Gtexp(Box::new(relexp), addexp, "gt".to_string()),
    <relexp: RelExp> "<=" <addexp: AddExp> => RelExp::Leexp(Box::new(relexp), addexp, "le".to_string()),
    <relexp: RelExp> ">=" <addexp: AddExp> => RelExp::Geexp(Box::new(relexp), addexp, "ge".to_string()),
}

// EqExp       ::= RelExp | EqExp ("==" | "!=") RelExp;
EqExp: EqExp = {
    <relexp: RelExp> => EqExp::Relexp(relexp),
    <eqexp: EqExp> "==" <relexp: RelExp> => EqExp::Eqexp(Box::new(eqexp), relexp, "eq".to_string()),
    <eqexp: EqExp> "!=" <relexp: RelExp> => EqExp::Neqexp(Box::new(eqexp), relexp, "ne".to_string()),
}

// LAndExp     ::= EqExp | LAndExp "&&" EqExp;
LAndExp: LAndExp = {
    <eqexp: EqExp> => LAndExp::Eqexp(eqexp),
    <landexp: LAndExp> "&&" <eqexp: EqExp> => LAndExp::Andexp(Box::new(landexp), eqexp),
}

// LOrExp      ::= LAndExp | LOrExp "||" LAndExp;
LOrExp: LOrExp = {
    <landexp: LAndExp> => LOrExp::Landexp(landexp),
    <lorexp: LOrExp> "||" <landexp: LAndExp> => LOrExp::Orexp(Box::new(lorexp), landexp),
}

// Number      ::= INT_CONST;
Number: i32 = <num: IntConst> => <>;


// 如果匹配到标识符, 就返回这个字符串
// 一对尖括号在此处指代的是正则表达式匹配到的字符串 (&str)
// 关于尖括号到底代表什么, 请 RTFM
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();


// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
    r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
    r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
    r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}